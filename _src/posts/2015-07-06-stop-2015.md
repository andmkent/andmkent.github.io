    Title: STOP 2015 Talk: Adding Practical Dependent Types to Typed Racket
    Date: 2015-07-06T09:16:07
    Tags: Talks, Typed Racket

At the
[Scripts to Programs 2015](http://2015.ecoop.org/track/STOP2015)
workshop (co-located with ECOOP 2015) I gave a talk reporting on our
upcoming addition of some basic dependent types to Typed Racket.

<!-- more -->

## Abstract

Typed Racket is a statically typed dialect of Racket that allows
idiomatic Racket programs to be enriched with types. It can reason
about many dynamically typed programming patterns while providing
sound interoperability and optimizations. We have designed and are
implementing an extension to Typed Racket which adds support for
logical refinement types and linear integer constraints. This summary
discusses our approach to implementing this novel combination of
precise specifications and optimizations while maintaining sound
interoperability with dynamically typed code. A PLT Redex model of the
basic calculus describing our extension can be seen at
https://github.com/andmkent/stop2015-redex and our development fork
where we are extending Typed Racket is available at
https://github.com/andmkent/typed-racket/tree/dtr-prototype.

## Overview of Additions to Typed Racket

This extension involves adding just a few simple forms to Typed Racket.

First, we need to add a type which can express dependencies within it. We do
this with refinement types:

```racket
(Refine [x : T] P(x))
```

This intuitively describes all values `x` of type `T` where proposition `P(x)` holds.

Today Typed Racket *already* reasons about some basic propositions:

* `(x -: T)`, `x` is of type `T`

* `(x -! T)`, `x` is not of type `T`

* `(and P ...)`, standard logical *and*

* `(or P ...)`, standard logical *or*

This is how Typed Racket can typecheck programs using type-tests in
`if` statements, etc...

Next we add propositions which can talk about linear inequalities:

* `((+ x (* 3 y)) ≤ 42)`, the linear combination `(+ x (* 3 y))` is less than
or equal to `42`.

These propositions can only reference linear combinations of integers (_e.g. no
1.5 or (* x y)_).


## Examples with Refinements and Dependent Types

Here are two examples of programs we can write with these new forms.
Both utilize `safe-vector-` operations, whose type requires the
index to be verified statically (and they can therefore omit
the runtime bounds-check).

### Vector Dot Product

```racket

(: dot-prod (~> [A : (Vecof Int)]
                [B : (Refine [v : (Vecof Int)]
                             (= (len v) (len A)))]
                Int))
(define (dot-prod A B)
  (for/sum ([i (in-range (len A))])
    (* (safe-vector-ref A i)
       (safe-vector-ref B i))))

```

### Matrix Multiplication

```racket

(define-syntax-rule (Mref M r c)
  (safe-vector-ref (safe-vector-ref M r) c))

(define-syntax-rule (Mset! M r c val)
  (safe-vector-set! (safe-vector-ref M r) c val))

;; A[m×n] * B[n×p] = C[m×p]) 
(: matrix*
   (~> [A : (Vec (Refine [a : (Vec Int)] (= (len a) n)))]
       [B : (Vec (Refine [b : (Vec Int)] (= (len b) p)))]
       [C : (Vec (Refine [c : (Vec Int)] (= (len c) p)))]
       [m : (Refine [x : Nat] (= x (len A) (len C)))]
       [n : (Refine [y : Nat] (= y (len B)))]
       [p : Nat]
       Void))
(define (matrix* A B C m n p)
  (for* ([i : (Refine [x : Nat] (< x m))
            (in-range m)]
         [j : (Refine [y : Nat] (< y p))
            (in-range p)])
    (define Cij-val : Int
      (for/sum ([k : (Refine [z : Nat] (< z n))
                   (in-range n)])
        (+ (Mref A i k) (Mref B k j))))
    (Mset! C i j Cij-val)))
```

## Vector Bounds Case Study

We performed a case study analyzing how many instances of `vector-ref`
and `vector-set!` we could automatically verify without any
modifications to the code. In approximately 57k lines of code drawn
from Racket's [math](http://docs.racket-lang.org/math/),
[plot](http://docs.racket-lang.org/plot/index.html?q=plot), and
[pict3d](https://github.com/ntoronto/pict3d) libraries we successfully
could verify the vector operations about 50% of the time.

We will report in more detail on these findings in an upcoming publication.
